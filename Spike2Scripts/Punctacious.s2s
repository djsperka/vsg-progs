' $Id: Punctacious.s2s,v 1.1 2011-03-29 06:34:20 devel Exp $
'
#include "../../Spike2Util/UsreyUtil.s2s"
#include "PunctaciousDialog.s2s"

' Channel numbers
var punChanTrigger% := 11;
var punChanVSGReady% := 12;

' Windows
var punDataWindow%;
var punRasterWindow%[8];    ' one for each electrode

' Handle
var punStimHandle;

' trigger counting
var punNTriggersCounted% := 0;
var punNTriggersExpected% := 0;
var punLastTriggerTime := -1;
var punFirstTriggerTime := -1;

' states
const stateStartup%             :=  1;
const stateVSGWait%             :=  2;
const stateVSGConfirm%          :=  3;
const stateStartStim%           :=  4;
const stateStim%                :=  5;
const stateDone%                :=  6;

var iState% := stateStartup%;              ' This holds the current state

'~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
' Script starts here

LogInit(1);

if PunctaciousDialog%() = 0 then
    halt;
endif

' Sampling config
CreateSampleConfiguration();

' Prepare display windows, input channels, etc. 
InitializeWindows();

' Establish matlab connection
'if MatlabOpen() < 0 then
'    LogError("", "MatlabOpen failed.");
'else
'    MatlabShow(1);
'endif

 
' Set up toolbar and launch
InitializeToolbar();




proc CreateSampleConfiguration()
    
    var i%;
    
	SampleClear(); 'Set standard sampling state
	SampleAutoComment(0); 'Prompt for comment after sampling
	SampleAutoCommit(60); 'Seconds between each file commit
    
    FilePathSet(GetDataPathRoot$() + "\\" + GetDataSetName$(), 3);
    FilePathSet(GetDataPathRoot$() + "\\" + GetDataSetName$(), 1);
    SampleAutoName$(dataCellName$ + "_pun_000");
	SampleAutoFile(1);

    'Channel recording definitions

    for i% := 1 to punNChannels% do
        SampleWaveMark(i%, i%-1, 2000, 34, 15, 28000); ' chan, port, event rate, points, pre-trigger, rate
        SampleTitle$(i%, "Trode " + Str$(i%-1));
    next

    SampleEvent(punChanTrigger%, 1, 1, 1000); 
    SampleTitle$(punChanTrigger%, "Terms");
    
   	SampleEvent(punChanVSGReady%, 6, 2, 200); 'Trigger channel, level
	SampleComment$(punChanVSGReady%, "VSG Rdy");
	SampleTitle$(punChanVSGReady%, "VSG Rdy");
    
	SampleSequencer(script$ + "SparseNoise.pls");
    SampleOptimise(2, 2, 2, 2, 50);  'Set optimise mode (do this last)
    SampleMode(1); 'Continuous sampling
    
end;


proc InitializeWindows()
    var i%;
    var AutoCorr%;
	var binsz;
    var winsz;
    
    View(App(8)).WindowVisible(0);	'Hide the standard sample bar    
    punDataWindow%:=FileNew(0,4);
    View(punDataWindow%).WindowVisible(1);
    XRange(0, 15);
    View(punDataWindow%).Window(0,0,50,100);
    
    winsz := floor(100/punNChannels%);
    if punUseRamp% = 1 then
        binsz := 1/punFreq;        
        for i% := 0 to punNChannels%-1 do
            punRasterWindow%[i%] := View(punDataWindow%).SetPSTH(str$(i%+1), 1, binsz, 0, punChanTrigger%, 2);
            View(punRasterWindow%[i%]);
            WindowVisible(1);
            Window(50, i%*winsz, 100, winsz);
        next
    else
        binsz := punTOff + punTOn;
        for i% := 0 to punNChannels%-1 do
            punRasterWindow%[i%] := View(punDataWindow%).SetPSTH(str$(i%+1), 1, binsz, punTOff, punChanTrigger%, 2);
            View(punRasterWindow%[i%]);
            WindowVisible(1);
            Window(50, i%*winsz, 100, winsz);
        next
    endif

end


proc InitializeToolbar()
	
    ToolbarSet(1,"Quit",Quit%);
    ToolbarSet(2,"Sample start",Start%);
    ToolbarSet(3,"Sample stop", Stop%);
    ToolbarEnable(1, 1);
    ToolbarEnable(2, 1);
    ToolbarEnable(3, 0);
    Toolbar("Go",0xffff);
    
end

func Start%()
    var cmd$;
    var args$;
    cmd$ := "cmd /c " + GetBinDir$(1) + "punctate.exe ";
    args$ := " -r 16 -c 16 -d " + str$(punDotSize%);
    
    ' contrast ramp or on/off
    if punUseRamp% = 1 then
        args$ += " -f " + str$(punFreq);
    else
        args$ += " -t " + str$(punTOn) + "," + str$(punTOff);
    endif
    
    ' full field?
    if punFullField% = 1 then
        args$ += " -F ";
    endif
    
    ' grid points?
    if punUseGrid% = 1 then
        args$ += " -p " + str$(punGridCenterX%) + "," + str$(-punGridCenterY%);
        if punUseDot1% = 1 then
            args$ += " -P " + str$(punDot1XY%[0]) + "," + str$(punDot1XY%[1]);
        endif
        if punUseDot2% = 1 then
            args$ += " -P " + str$(punDot2XY%[0]) + "," + str$(punDot2XY%[1]);
        endif
    endif
    
    ' free points?
    if punUsePoint1% = 1 then
        args$ += " -Z " + str$(punPoint1XY[0]) + "," + str$(-punPoint1XY[1]);
    endif
    if punUsePoint2% = 1 then
        args$ += " -Z " + str$(punPoint2XY[0]) + "," + str$(-punPoint2XY[1]);
    endif
    
    cmd$ += args$;
    
    LogInfo(cmd$);
    punStimHandle := ProgRun(cmd$);
    
    SampleStart(0);
    ToolbarEnable(1,0); 
    ToolbarEnable(2,0);
    ToolbarEnable(3,1);
    ToolbarSet(0, "", Idle%);
    return 1;
end

func Stop%()
    
    SampleKey("q");
    Yield();
    SampleStop();
    ToolbarSet(0, "");
    ToolbarEnable(1,1); 
    ToolbarEnable(3,0);
    return 1;
    
end

func Quit%()
    ' this would have means to stop MSgSvr, like msequence. 
    return 0;
end


var tTrigger := -1;
var tLast := -1;
var tNow := -1;
var tLastTrigger := -1;
var vTrigger;
var tStateStart := -1;
var pctComplete;
var timeLeft;
var nc, ne; ' spike2 doesn't have a cast operator? 
var tLastUpdate := -1;

func Idle%()
    var status% := 1;
    var i%;
	tNow := View(punDataWindow%).MaxTime();
    
    docase 
    case iState% = stateStartup% then
        
        LogStatus("stateStartup", "Starting up.");
        ChangeStateTo(stateVSGWait%, tNow);
        
    case iState% = stateVSGWait% then
        
        ' Waiting for VSG to indicate it is ready. The indication is a pulse (up, then down, 1 frame apart) on port 6.
        tTrigger := View(punDataWindow%).NextTime(punChanVSGReady%, tLast, vTrigger);
        if tTrigger > tLastTrigger and vTrigger = 0 then
            LogStatus("stateVSGWait", "Got leading edge of trigger.");
            tLastTrigger := tTrigger;
            ChangeStateTo(stateVSGConfirm%, tTrigger);                
        endif;
            
    case iState% = stateVSGConfirm% then
            
        ' Waiting for VSG to confirm it is ready. The downward side of the pulse is expected...
        tTrigger := View(punDataWindow%).NextTime(punChanVSGReady%, tLastTrigger, vTrigger);
        if tTrigger > tLastTrigger and vTrigger = 1 then                
            LogStatus("stateVSGConfirm", "Got trailing edge of trigger.");
            tLastTrigger := tTrigger;
            ChangeStateTo(stateStartStim%, tTrigger);
        endif;
            
    case iState% = stateStartStim% then
        
        ' Issue sample key to start stim, then switch to looking for triggers
        LogStatus("stateStartStim", "Starting stimulus.");
        Yield(1.0);
        SampleKey("s");
        ChangeStateTo(stateStim%, tNow);
        punLastTriggerTime := tNow;
        
    case iState% = stateStim% then
        
        ' Call process for each trigger seen
        tTrigger := View(punDataWindow%).NextTime(punChanTrigger%, punLastTriggerTime);
        while tTrigger > -1 do
            if punNTriggersCounted% > 5 then
                LogStatus("stateStim", "Process trigger at " + str$(tTrigger));
                for i% := 1 to punNChannels% do
                    View(punRasterWindow%[i%-1]).Process(punLastTriggerTime, tTrigger, 0, 1);
                next
            endif
            punLastTriggerTime := tTrigger;
            punNTriggersCounted% += 1;
            tTrigger := View(punDataWindow%).NextTime(punChanTrigger%, punLastTriggerTime);
        wend
        
    case iState% = stateDone% then
        
        Stop%();
        status% := 1;
        
    else
        
        LogError("", "Unknown state " + str$(iState%));
        Stop%();
        status% := 0;
        
    endcase
    
    return status%;
end

proc ChangeStateTo(stateTo%, tStart)
	iState% := stateTo%;
	if tStart > 0 then tStateStart := tStart; endif;
end;


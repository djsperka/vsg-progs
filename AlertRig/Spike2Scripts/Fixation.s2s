' $Log: not supported by cvs2svn $
' Revision 1.13  2006/04/12 00:23:31  dan
' Moved vsg trigger to port 2
'
' Revision 1.12  2006/04/11 00:22:11  dan
' Changed to use release version of fixation program.
'
' Revision 1.11  2006/02/08 22:34:17  dan
' Allow modification of xy axes during data aquisition
'
' Revision 1.10  2005/09/21 22:51:21  dan
' Added handling for VSG ready pulse. Removed Yield(5) in Start function.
'
' Revision 1.9  2005/09/21 19:36:55  dan
' DoSampleKey correctly sends for both Fixation and Calibration runs.
'
' Revision 1.8  2005/05/03 00:06:04  dan
' Corrected bug in automatic file saving.
'
' Revision 1.7  2005/04/27 21:23:18  dan
' changed FixationColor to string in registry.
'
' Revision 1.6  2005/03/18 19:38:51  dan
' Merged calibration functionality in to this script.
'
' Revision 1.5  2005/03/11 01:13:12  dan
' Almost there!
'
' Revision 1.4  2005/03/09 01:06:48  dan
' Almost there!
'
' Revision 1.3  2005/03/05 01:08:54  dan
' Integrated cut-and-paste from Config.s2s.
' Still needs to be coupled to new Fixation app.
'
' Revision 1.2  2005/02/22 22:21:21  dan
' New fixation application added
'
' Revision 1.1  2005/02/19 01:42:21  dan
' Initial add.
'
' Revision 1.3  2004/10/01 21:07:51  dan
' Fix bugs in prog command line.
'
' Revision 1.2  2004/10/01 20:06:10  dan
' Modified directory locations for executables (../bin) and sequencer files (script dir)
'
' Revision 1.1  2004/10/01 19:41:01  dan
' From production
' 


'========================================================================
'
' INPUTS
'
' Eye X, Y - ADC input 14, 15 respectively
' VSG trigger - assumed on DIGIN byte 0.  *** THE 1401 SHOULD HAVE PREFERENCES 
'               SET WITH THE "Event Ports 0 and 1 on read digital input connector" 
'               BOX CHECKED!!!
'
'========================================================================

' File system reference vars. Each var is a directory path, WITHOUT the trailing slash. 
var bin$, script$, config$;
script$ := View(App(3)).FileName$(1) + View(App(3)).FileName$(2);	' folder containing this script
bin$ := View(App(3)).FileName$(1) + View(App(3)).FileName$(2) + "..\\bin";	' Location of executables is ..\bin, relative to script dir
config$ := View(App(3)).FileName$(1) + View(App(3)).FileName$(2) + "..\\config";	' Location of executables is ..\bin, relative to script dir

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Get Current configuration START
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func GetCurrentConfiguration$()
	var key$;
	var s$;
	key$:="AlertRig";
	Profile(key$, "CurrentConfiguration", "default", s$);
	return s$;
end;


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Get Current configuration END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Data parameters START
'
'	Vars that control where data is stored. 
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

var dataPathRoot$;		' base path for data storage
var dataDataSet$;		' folder under data path where data files placed
var dataCellName$;		' part of base name for all data files
var dataDataSetTextItem%;	' 

proc GetDataParameters()
	var stmp$;
	var key$;

	key$:="AlertRig\\Data";

	Profile(key$, "PathRoot", "C:\\Experiments", dataPathRoot$);
	Profile(key$, "DataSet", "", dataDataSet$);
	Profile(key$, "CellName", "", dataCellName$);
	return;
end;

proc SaveDataParameters()
	var stmp$;
	var key$;

	key$:="AlertRig\\Data";

	Profile(key$, "PathRoot", dataPathRoot$);
	Profile(key$, "DataSet", dataDataSet$);
	Profile(key$, "CellName", dataCellName$);
	return;
end;

func DataParametersDialog%()
	var i%;
	GetDataParameters();
	DlgCreate("Data Storage Parameters", 25, 10, 50);
	DlgText("Data root path: " + dataPathRoot$, 0, 1);
	dataDataSetTextItem% := DlgText("Data set name: " + dataDataSet$, 0, 2);
	DlgString(1, "Cell name:", 10, "", 0, 3);
        DlgButton(2, "New Data Set", NewDataSet%);
	i% := DlgShow(dataCellName$);
	if (i% = 1) then
		if Len(dataDataSet$) = 0 then
			Message("Must specify a data set (try the button!)");
		else
			if Len(dataCellName$) = 0 then
				Message("Must specify a cell name");
			else
				FilePathSet(dataPathRoot$ + "\\" + dataDataSet$, 3, 1);
				SaveDataParameters();
			endif;
		endif;
	endif;
	return i%;
end;


func NewDataSet%()
	var d$;
	var i%;
	var dirs$[100];	' if there are ever more than 100 datasets in a single day there will be trouble
	d$ := Date$(2, 2, 3, 3, "-");
	PrintLog("NewDataSet: date string = " + d$ + "\n");

	' Change current dir to data path root, then get list of subdirs with this date string
	PrintLog("NewDataSet: dataPathRoot$=" + dataPathRoot$ + "\n");
	FilePathSet(dataPathRoot$, 0, 1);
	i% := FileList(dirs$, -2, d$ + "-??");
        PrintLog("NewDataSet: found " + str$(i%) + " dirs\n");

	' form new dataset name. The first will have "-01" appended, the second "-02", etc. 
	dataDataSet$ := Print$("%s-%02d", d$, i%+1);
	PrintLog("NewDataSet: dataset= " + dataDataSet$ + "\n");
	DlgValue$(dataDataSetTextItem%, "Data set name: " + dataDataSet$);
	return 1;
end;



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' Data parameters END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters START
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


var DistanceToScreenMM$;		' Distance to screen in MM
var DegreesPerVoltX;			' Conversion factor from ADC to visual degrees
var DegreesPerVoltY;			' Conversion factor from ADC to visual degrees
var EyeCoilSamplingFrequency%;		' Frequency to sample eye coil at
var NumberOfElectrodes%;		' Number of electrodes (i.e. wavemark inputs) in use
var JuicePerReward%;			' Number of juice squirts per reward


func DAQParametersDialog%()

	var iReturn%;
	var idist%;

	GetDAQParameters();

	idist% := val(DistanceToScreenMM$);
	DlgCreate("DAQ parameters: " + GetCurrentConfiguration$());
	DlgInteger(1, "Number of Electrodes:", 1, 7, 0, 0, 1);
	DlgInteger(2, "Screen Distance (MM):", 1, 5000);
	DlgReal(3, "Degrees/Volt (X):", 0, 100);
	DlgReal(4, "Degrees/Volt (Y):", 0, 100);
	DlgInteger(5, "Eye Coil Sampling Freq (Hz):", 1, 20000);
	DlgInteger(6, "Juice per reward:", 0, 10, 0, 0, 1);
	iReturn% := DlgShow(NumberOfElectrodes%, idist%, DegreesPerVoltX, DegreesPerVoltY, EyeCoilSamplingFrequency%, JuicePerReward%);

	if iReturn% = 1 then
		DistanceToScreenMM$ := str$(idist%);
		SaveDAQParameters();
	endif
	return iReturn%;
end;




	
proc GetDAQParameters()
	var key$;
	var DegreesPerVoltX$, DegreesPerVoltY$;

	key$ := "AlertRig\\" + GetCurrentConfiguration$() + "\\DAQ";
	Profile(key$, "DistanceToScreenMM", "850", DistanceToScreenMM$);	' will just pass this to stimulus app
	Profile(key$, "DegreesPerVoltX", "", DegreesPerVoltX$);
	Profile(key$, "DegreesPerVoltY", "", DegreesPerVoltY$);
	Profile(key$, "EyeCoilSamplingFrequency", 1000, EyeCoilSamplingFrequency%);
	Profile(key$, "NumberOfElectrodes", 1, NumberOfElectrodes%);
	Profile(key$, "JuicePerReward", 1, JuicePerReward%);
	DegreesPerVoltX := val(DegreesPerVoltX$);
	DegreesPerVoltY := val(DegreesPerVoltY$);

end;

proc SaveDAQParameters()
	var key$;
	var DegreesPerVoltX$, DegreesPerVoltY$;
	DegreesPerVoltX$ := str$(DegreesPerVoltX);
	DegreesPerVoltY$ := str$(DegreesPerVoltY);
	
	key$ := "AlertRig\\" + GetCurrentConfiguration$() + "\\DAQ";
	Profile(key$, "DistanceToScreenMM", DistanceToScreenMM$);
	Profile(key$, "DegreesPerVoltX", DegreesPerVoltX$);
	Profile(key$, "DegreesPerVoltY", DegreesPerVoltY$);
	Profile(key$, "EyeCoilSamplingFrequency", EyeCoilSamplingFrequency%);
	Profile(key$, "NumberOfElectrodes", NumberOfElectrodes%);
	Profile(key$, "JuicePerReward", JuicePerReward%);

end;

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Fixation Point parameters START
'
'	These vars and funcs control the parameters for the fixation point stored in the registry. 
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''



var WindowRadius;
var FixationX, FixationY;
var FixationColor$;
var iFixationColor%;
var FixationDiameter;
var FixationColorList$[3]; 
var FixationArg$;

proc GetFixationPointParameters()
	var stmp$;
	var key$;
	key$ := "AlertRig\\" + GetCurrentConfiguration$() + "\\FixationPoint";
	Profile(key$, "WindowRadius", "1", stmp$);
	WindowRadius := val(stmp$);

	Profile(key$, "FixationX", "0", stmp$);
	FixationX := val(stmp$);
	Profile(key$, "FixationY", "0", stmp$);
	FixationY := val(stmp$);
	Profile(key$, "FixationColor", "0", stmp$);
	iFixationColor% := val(stmp$);

	FixationColorList$[0]:="red"; 
	FixationColorList$[1]:="green"; 
	FixationColorList$[2]:="blue"; 
	FixationColor$ := FixationColorList$[iFixationColor%];

	Profile(key$, "FixationDiameter", "0.5", stmp$);
	FixationDiameter := val(stmp$);

	' Form the argument for applications. Convenience for passing args. 
	FixationArg$ := " -f " + str$(FixationX) + "," + str$(FixationY) + "," + str$(FixationDiameter) + "," + FixationColor$;
end;

proc SaveFixationPointParameters()
	var key$;
	key$ := "AlertRig\\" + GetCurrentConfiguration$() + "\\FixationPoint";
	Profile(key$, "WindowRadius", str$(WindowRadius));
	Profile(key$, "FixationX", str$(FixationX));
	Profile(key$, "FixationY", str$(FixationY));
	Profile(key$, "FixationColor", str$(iFixationColor%));
	Profile(key$, "FixationDiameter", str$(FixationDiameter));
end;


func FixationPointDialog%()

	var i%;
	GetFixationPointParameters();
	DlgCreate("Fixation parameters: " + GetCurrentConfiguration$(), 25, 10);
	DlgReal(1, "Fixation X:", -20, 20);
	DlgReal(2, "Fixation Y:", -20, 20);
	DlgReal(3, "Fixation diam:", .1, 5);
	DlgReal(4, "Window radius:", .1, 10);
	DlgList(5, "Color:", FixationColorList$[]);
	i% := DlgShow(FixationX, FixationY, FixationDiameter, WindowRadius, iFixationColor%);	

	' If user cancelled, reload params
	if i% = 0 then
		GetFixationPointParameters();
	else
		SaveFixationPointParameters();
	endif

	return 1;
end;


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Fixation Point parameters END
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''




'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' Fixation script parameters START
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Fixation-script specific control vars.
var fixFixationDuration;			' Time required to maintain fixation, seconds
var fixAcquisitionTime;			' Time allowed to acquire target before fail trial, seconds
var fixInterTrialTime;			' Blank time following a trial (both successful and failed), seconds
var fixNumberOfTrials%;			' Number of trials to run
var fixStimulusFlag%;			' Tells whether to use stimulus grating (used as a distractor) 
var fixDistractor1Flag%; 			' Tells stim to use distractor1 (1) or not (0)
var fixDistractor2Flag%; 			' Tells stim to use distractor1 (1) or not (0)
var fixDistractor3Flag%; 			' Tells stim to use distractor1 (1) or not (0)
var fixDistractor4Flag%; 			' Tells stim to use distractor1 (1) or not (0)
var fixBackgroundColor$;			' Background color for stim
var fixCalibrationFlag%;			' 1 for calibration run
var fixCalibrationOffset;			' degree offset for calibration dots
proc GetFixationScriptParameters()
	var stmp$;
	var key$;

	key$:="AlertRig\\" + GetCurrentConfiguration$() + "\\Scripts\\Fixation";

	Profile(key$, "FixationDuration", "1.0", stmp$);
	fixFixationDuration := val(stmp$);

	Profile(key$, "AcquisitionTime", "1.0", stmp$);
	fixAcquisitionTime := val(stmp$);

	Profile(key$, "InterTrialTime", "1.0", stmp$);
	fixInterTrialTime := val(stmp$);

	Profile(key$, "NumberOfTrials", 1, fixNumberOfTrials%);
	Profile(key$, "BackgroundColor", "gray", fixBackgroundColor$);

	Profile(key$, "UseStimulusFlag", 0, fixStimulusFlag%);
	Profile(key$, "UseDistractor1Flag", 0, fixDistractor1Flag%);
	Profile(key$, "UseDistractor2Flag", 0, fixDistractor2Flag%);
	Profile(key$, "UseDistractor3Flag", 0, fixDistractor3Flag%);
	Profile(key$, "UseDistractor4Flag", 0, fixDistractor4Flag%);
	Profile(key$, "DoCalibrationFlag", 0, fixCalibrationFlag%);
	Profile(key$, "CalibrationOffset", "5.0", stmp$);
	fixCalibrationOffset := val(stmp$);
end;
 	
proc SaveFixationScriptParameters()
	var stmp$;
	var key$;
	key$:="AlertRig\\" + GetCurrentConfiguration$() + "\\Scripts\\Fixation";

	Profile(key$, "FixationDuration", str$(fixFixationDuration));
	Profile(key$, "AcquisitionTime", str$(fixAcquisitionTime));
	Profile(key$, "InterTrialTime", str$(fixInterTrialTime));
	Profile(key$, "NumberOfTrials", fixNumberOfTrials%);
	Profile(key$, "BackgroundColor", fixBackgroundColor$);
	Profile(key$, "UseStimulusFlag", fixStimulusFlag%);
	Profile(key$, "UseDistractor1Flag", fixDistractor1Flag%);
	Profile(key$, "UseDistractor2Flag", fixDistractor2Flag%);
	Profile(key$, "UseDistractor3Flag", fixDistractor3Flag%);
	Profile(key$, "UseDistractor4Flag", fixDistractor4Flag%);
	Profile(key$, "DoCalibrationFlag", fixCalibrationFlag%);
	Profile(key$, "CalibrationOffset", str$(fixCalibrationOffset));
end;



func FixationScriptDialog%()
	var i%;
	var bcl$[3];
	var ibcl%:=0;
	GetFixationScriptParameters();
	bcl$[0] := "gray";
	bcl$[1] := "black";
	bcl$[2] := "white";
	for i%:=0 to 2 do
		if fixBackgroundColor$ = bcl$[i%] then
			ibcl% := i%;
		endif
	next;
	DlgCreate("Fixation Training Parameters", 25, 10);
	DlgReal(1, "Fixation duration(s):", .01, 10);
	DlgReal(2, "Acquisition time(s):", .01, 10);
	DlgReal(3, "Intertrial time(s):", .01, 10);
	DlgInteger(4, "Number of Trials:", 1, 1000);
	DlgList(5, "Background color:", bcl$);
	DlgCheck(6, "Use stimulus?");
	DlgCheck(7, "Use distractor 1?");
	DlgCheck(8, "Use distractor 2?");
	DlgCheck(9, "Use distractor 3?");
	DlgCheck(10, "Use distractor 4?");
	DlgCheck(11, "Calibration run?");
	DlgReal(12, "Calibration offset (deg):", 0, 10);
	i%:=DlgShow(fixFixationDuration, fixAcquisitionTime, fixInterTrialTime, fixNumberOfTrials%, ibcl%, fixStimulusFlag%, fixDistractor1Flag%, fixDistractor2Flag%, fixDistractor3Flag%, fixDistractor4Flag%, fixCalibrationFlag%, fixCalibrationOffset);
	if i% = 1 then
		fixBackgroundColor$ := bcl$[ibcl%];
		SaveFixationScriptParameters();
	endif;
	return 1;
end;

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' Fixation script END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Rig Daily Totals START
'
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


var TotalTrials%;
var TotalRewards%;
var TotalDate$;


proc GetDailyTotals()
	var key$;
	key$ := "AlertRig\\Daily";
	Profile(key$, "TotalDate", "", TotalDate$);
	Profile(key$, "TotalTrials", 0, TotalTrials%);
	Profile(key$, "TotalRewards", 0, TotalRewards%);

	' Get date string if needed
	if Len(TotalDate$) = 0 then
		TotalDate$ := GetDayString$();
		SaveDailyTotals();
	endif
end;

proc SaveDailyTotals()
	var key$;
	key$ := "AlertRig\\Daily";
	Profile(key$, "TotalDate", TotalDate$);
	Profile(key$, "TotalTrials", TotalTrials%);
	Profile(key$, "TotalRewards", TotalRewards%);
end;

func DailyTotalsDialog%()
	GetDailyTotals();
	DlgCreate("Daily Totals");
	DlgText("Date: " + TotalDate$, 0, 1);
	DlgText("Trials: " + str$(TotalTrials%), 0, 2);
	DlgText("Rewards: " + str$(TotalRewards%), 0, 3);
	DlgButton(2, "Reset", ResetDailyTotals%);
	DlgShow();
	return 1;
end;


func GetDayString$()
	return Date$(2, 1, 3, 2, "-");
end;


func ResetDailyTotals%()
	GetDailyTotals();
	TotalRewards%:=0;
	TotalTrials%:=0;
	TotalDate$:= GetDayString$();
	SaveDailyTotals();
        return 0;
end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	Rig Daily Totals END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''







' Window handles and channel numbers
var XChannel%,YChannel%;		' Channel number assigned to eye x and y
var TriggerChannel%;			' Channel number assigned to vsg trigger input (i.e. trigger sent by vsg)
var ReadyChannel%;			' channel number for VSG ready pulse signal
var DataWindow%;	' time view
var XYWindow%;		' window with eye pos shown
var iStimChannel%;	' channel number for stimulus in xy window
var iStimWindowChannel%;' channel number for stimulus window in xy window
var iMonitorChannel%;	' channel number for monitor position display in xy window
var StimHandle%;	' handle for stimulus application
' Experimental control
var Pause%:=0;		' When 1, expt is paused - no idle processing done

' Display stuff
var FixWindowX[36],FixWindowY[36];	' points for displaying fixation window in xy view
var windowX[36], windowY[36];
' Stimulus location stuff
var iStimX%[8];
var iStimY%[8];
var iStimIndex%;





' Fetch parameters
GetDailyTotals();
GetDAQParameters();
GetFixationPointParameters();
GetFixationScriptParameters();
GetDataParameters();


' Check for valid data file path
if Len(dataDataSet$) = 0 then
	Message("No data set is selected! Edit Data parameters...");
	' Might be nice to open the dialog here....
	halt;
else 
	' Set path for new data files
	FilePathSet(dataPathRoot$ + "\\" + dataDataSet$, 3, 1);
	FilePathSet(dataPathRoot$ + "\\" + dataDataSet$, 1, 1);
	SampleAutoName$(dataCellName$ + "_fix_000");
	SampleAutoFile(1);
	PrintLog("dataPathRoot=" + dataPathRoot$ + "\n");
	PrintLog("dataDataSet=" + dataDataSet$ + "\n");
	PrintLog("AutoFile status " + str$(SampleAutoFile()) + "\n");
endif





' Assign channel numbers for eye X,Y and trigger. 
XChannel% := NumberOfElectrodes%+1;
YChannel% := NumberOfElectrodes%+2;
TriggerChannel% := NumberOfElectrodes%+3;
ReadyChannel% := NumberOfElectrodes% + 4;
CreateSampleConfiguration();


' Get display windows ready
InitializeWindows();

' Prepare the toolbar buttons
InitializeToolbar();







const stateStartTrial%		:= 0;
const stateWaitForStim%		:= 1;
const stateWaitForAcquisition%	:= 2;
const stateHoldFixation%	:= 3;
const stateSuccess%		:= 4;
const stateFailure%		:= 5;
const stateStimOff%		:= 6;
const stateWaitForBlank%	:= 7;
const stateCheckTrial%		:= 8;
const stateInterval%		:= 9;
const stateDone%		:= 10;
const stateVSGWait%:=10;		' wait for vsg to send ready signal #1
const stateVSGWaitConfirm%:=11;		' waif for vsg to send ready signal #2


var tNow:=0;		' latest time for which there are values recorded. Used in IdleProcessing
var tLast:=-1;		' last time we checked for a trigger. 
var xEye, yEye;		' eye positions
var tLastUpdate:=-1;	' last time view was updated
var iState%:=stateVSGWait%;		' present state
var vTrigger;		' set by NextTime to indicate trigger transition
var tStateStart:=-1;	' time that present state started. Used to measure wait time, fixation time, etc. 
var tLastTrigger:=-1;	' last trigger time. New triggers have to be greater than this
var tTrigger:=-1;	' temp var for newly discovered trigger times
var nTrialsStarted%:=0;
var nTrialsSuccess%:=0;
var nTrialsFail%:=0;

' Run real time analysis here. 
func IdleProcessing%()
	var iStatus% := 1;	' this is return value. value of 1 (0) continues (stops) processing
	tNow := MaxTime();

	xEye := ChanMeasure(XChannel%,2,tNow-.001,tNow-.006)*DegreesPerVoltX;
	yEye := ChanMeasure(YChannel%,2,tNow-.001,tNow-.006)*DegreesPerVoltY;

	' Don't update display every time....
	if tNow-tLastUpdate > .005 then
		View(XYWindow%).XYAddData(1,xEye,yEye);
		tLastUpdate := tNow;
	endif

	' What state are we in? 
	docase 

	
		case iState% = stateVSGWait% then

			' Waiting for VSG to indicate it is ready. The indication is a pulse (up, then down, 1 frame apart) on port 6.
			tTrigger := NextTime(ReadyChannel%, tLast, vTrigger);
			if tTrigger > tLastTrigger and vTrigger = 0 then
				PrintLog("state stateVSGWait: Got leading edge of trigger\n");
				tLastTrigger := tTrigger;
				iState% := stateVSGWaitConfirm%;
			endif;

		case iState% = stateVSGWaitConfirm% then

			' Waiting for VSG to confirm it is ready. The downward side of the pulse is expected...
			tTrigger := NextTime(ReadyChannel%, tLastTrigger, vTrigger);
			if tTrigger > tLastTrigger and vTrigger = 1 then
				PrintLog("state stateVSGWaitConfirm: Got trailing edge of trigger\n");
				tLastTrigger := tTrigger;
				iState% := stateStartTrial%;
			endif;

	
		case iState% = stateStartTrial% then

			' initial state on startup. 
			' DIGOUT to present stimulus
			' Enter state 1

			nTrialsStarted% += 1;
			iStimIndex% := (iStimIndex% + 1) mod 8;

			' calibration run does not require a separate trigger - the setting of the seqvar does the trick. 
			if fixCalibrationFlag% = 1 then
				DoSampleKey();
			else
				SampleKey("H");
			endif
			iState% := stateWaitForStim%;
'			PrintLog("State 0 - Start new trial - stimulus trigger sent\n");
			UpdateToolbarText();

		case iState% = stateWaitForStim% then
	
			' Waiting for stimulus to be presented by vsg and trigger to return. 
			' NextTime returns a non-negative value whenever there is a TRANSITION. In that case, the 
			' value of 'vTrigger' returned is 1 (0) if the transition is high (low) to low (high). Maybe not
			' intuitive, but that's the way it is. 
			tTrigger := NextTime(TriggerChannel%,tLast,vTrigger);
			if tTrigger > tLastTrigger then

				if vTrigger = 0 then
		
					' Stimulus has been presented. Display stim location and window. Enter state 2
					DrawStim(1);
					iState% := stateWaitForAcquisition%;
					tStateStart := tTrigger;
					tLastTrigger := tTrigger;

				endif

			endif

		case iState% = stateWaitForAcquisition% then

			if tNow-tStateStart < fixAcquisitionTime then
				
				' Acquisition happens when the eye falls within the fixation window.
				if InFixationWindow%(xEye, yEye) > 0 then

'					PrintLog("State 2 - acquired target. tNow=" + str$(tNow) + " Enter state 3\n");
					iState% := stateHoldFixation%;
					tStateStart := tNow;
	
				endif

			else

				' Failed. Turn off stim and try again.
'				PrintLog("State 2 - acquisition failed. Enter state 4\n");
				iState% := stateFailure%;
				tStateStart := tNow;

			endif

		case iState% = stateHoldFixation% then

			' Waiting for fixation....
			if InFixationWindow%(xEye, yEye) <= 0 then

				iState% := stateFailure%;
				tStateStart := tNow;

			else

				if tNow - tStateStart >= fixFixationDuration then

					' Fixation. Reward. New trial.
					iState% := stateSuccess%;
					tStateStart := tNow;

				endif

			endif

		case iState% = stateSuccess% then

			nTrialsSuccess% += 1;
			Reward%();
			' tally successful trials
			iState% := stateStimOff%;
			tStateStart := tNow;

		case iState% = stateFailure% then

			nTrialsFail% += 1;
			iState% := stateStimOff%;
			tStateStart := tNow;

		case iState% = stateStimOff% then

			' Turn off stimulus, enter state 5 - wait for stimulus off msg
			DrawStim(0);
'			PrintLog("State 4 - turning off stimulus....\n");
			SampleKey("L");
			iState% := stateWaitForBlank%;
			tStateStart := tNow;

		case iState% = stateWaitForBlank% then

			' Wait for stim to be off
			tTrigger := NextTime(TriggerChannel%,tLast,vTrigger);
			if tTrigger > tLast then

				if vTrigger = 1 then
		
					' Stimulus has been turned off by vsg. Enter state 6
'					PrintLog("State 5 - Stim OFF trigger found. Enter state 6\n");
					iState% := stateCheckTrial%;
					tStateStart := tTrigger;
					tLastTrigger := tTrigger;
	
				endif

			endif

		case iState% = stateCheckTrial% then

			' Check trial count
			if nTrialsStarted% = fixNumberOfTrials%  and fixCalibrationFlag% = 0 then
				Stop%();
				iState% := stateDone%;
				iStatus% := 0;
			else
				iState% := stateInterval%;
				tStateStart := tNow;
			endif


		case iState% = stateInterval% then

			' Inter-trial wait time
			if tNow - tStateStart > fixInterTrialTime then
	
				' Back to state 0
'				PrintLog("State 6 - IT wait is done. Start new trial.\n");
				iState% := stateStartTrial%;
				tStateStart := tNow;
	
			endif

		else 

			' Unknown state!
			Message("Unknown state=" + str$(iState%));
			halt;

	endcase;

	tLast := tNow;
	
	return iStatus%;

end;


proc DoSampleKey()
	var iSeqVar% := 0;
	iSeqVar% := (3 + iStimY%[iStimIndex%]*16 + iStimX%[iStimIndex%]*4) * 256;
	SampleSeqVar(1, iSeqVar%);
	SampleKey("S");
end;

proc UpdateToolbarText()

	var s$;
	s$ := Print$("Trial#/Total %d/%d Success/Failure %d/%d", nTrialsStarted%, fixNumberOfTrials%, nTrialsSuccess%, nTrialsFail%);
	ToolbarText(s$);

end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
proc CreateSampleConfiguration()
	var index;
	SampleClear(); 'Set standard sampling state
'	SampleAutoFile(0); 'Enable automatic file naming
	SampleAutoComment(0); 'Prompt for comment after sampling
	SampleAutoCommit(60); 'Seconds between each file commit

	'Channel recording definitions
	SampleEvent(TriggerChannel%, 2, 2, 3600); 'Trigger channel, level
	SampleComment$(TriggerChannel%, "Trigger");
	SampleTitle$(TriggerChannel%, "Trigger");

	' channel to look for ready signal from vsg
	SampleEvent(ReadyChannel%,6,2,3600); 'Trigger channel, level
	SampleComment$(ReadyChannel%,"VSG Ready");
	SampleTitle$(ReadyChannel%,"VSG Ready");


	for index := 1 to NumberOfElectrodes% do
		SampleWaveMark(index,index-1,2000,34,15,28000); ' chan, port, event rate, points, pre-trigger, rate
		SampleTitle$(index, "Electrode_"+Str$(index)); 
	next

	'EYE COIL CHANNELS. Last # is sampling freq in Hz. 
	SampleWaveform(XChannel%, 14, 1000);
	SampleTitle$(XChannel%,"Eye X");
	SampleWaveform(YChannel%, 15, 1000);
	SampleTitle$(YChannel%, "Eye Y");

	SampleSequencer(script$ + "Fixation.pls");
	SampleOptimise(2, 2, 2, 2, 50);  'Set optimise mode (do this last)
	SampleMode(1); 'Continuous sampling
end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' DrawStim
' 
' Draws stimulus in xy window (iDraw%==1) or blanks it (0). 
proc DrawStim(iDraw%)

	if iDraw% > 0 then
		var xstim, ystim;
		xstim := GetStimX();
		ystim := GetStimY();

		' Draw stimulus in xy window
		View(XYWindow%).XYAddData(iStimChannel%, xstim, ystim);
	
		' draw fixation window
		ArrConst(windowX[], FixWindowX[]);
		ArrAdd(windowX[], xstim);
		ArrConst(windowY[], FixWindowY[]);
		ArrAdd(windowY[], ystim);

		View(XYWindow%).XYAddData(iStimWindowChannel%, windowX[], windowY[]);		
	else 
		' erase the stim and the window from xy view
		View(XYWindow%).XYDelete(iStimChannel%);
		View(XYWindow%).XYDelete(iStimWindowChannel%);
	endif

end;


proc InitializeWindows()

	'Open the data sampling window
	DataWindow% := FileNew(0,4);
	Window(-0.235849,52.746,97.2484,99.8856);
	View(DataWindow%).WindowVisible(1);

	'Open an XY view to display eye position
	XYWindow% := FileNew(12);
	XRange(-30,30);
	YRange(-2,-30,30);
	XYDrawMode(0,5,0);
	WindowVisible(1);
	Window(-0.550314,0.114416,41.5094,52.746);
	XYColour(1,16);
	XYSize(1,-1);

	'Calcuate the fixation window
	var index%;
	var twopi;
	twopi := 2.0*4.0*ATan(1.0);
	
	for index% := 0 to 35 do
		FixWindowX[index%] := WindowRadius * Cos(index% * twopi/36.0);
		FixWindowY[index%] := WindowRadius * Sin(index% * twopi/36.0);
	next;

	'Create a new channel in the XY view to display the fixation window
	iStimWindowChannel% := XYSetChan(0);
	XYColour(2,13);
	XYDrawMode(2,2,1);
	XYJoin(2,1);

	'Draw the fixation window ON SECOND THOUGHT, DON'T
	'XYAddData(2, FixWindowX[], FixWindowY[]);	

	'Create a new channel in the XY view to display the fixation point
	iStimChannel% := XYSetChan(0);

	'Create a new channel in the XY view to show the monitor's location
	iMonitorChannel% := XYSetChan(0);
	XYColour(4,13);
	XYDrawMode(4,3,1);
	XYJoin(4,2);
	XYAddData(4,-9,-7);
	XYAddData(4,9,-7);
	XYAddData(4,9,7);
	XYAddData(4,-9,7);

end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' InitilizeToolbar - prepare toolbar buttons. Note that the idle processing function (ToolbarSet(0, blah, blah)) is 
' not set here. That's done when the user hits the start button.
'
proc InitializeToolbar()

	View(App(8)).WindowVisible(0);	'Hide the standard sample bar
	ToolbarVisible(1);
	ToolbarSet(1,"Sample start",Start%);
	ToolbarSet(2,"Sample stop", Stop%);
	ToolbarSet(3,"Juice",Reward%);
	ToolbarSet(4,"Pause stimuli",PauseStimuli%);
	ToolbarSet(5,"Resume stimuli",ResumeStimuli%);
	ToolbarSet(6,"Exp Parameters",FixationScriptDialog%);
	ToolbarSet(7,"Horizontal", Horizontal%);
	ToolbarSet(8,"Vertical", Vertical%);
	ToolbarSet(9,"Corners", Corners%);
	ToolbarSet(10,"Quit",Quit%);
	ToolbarEnable(2,0);
	ToolbarEnable(4,0);
	ToolbarEnable(5,0);
	ToolbarEnable(7,0);
	ToolbarEnable(8,0);
	ToolbarEnable(9,0);
	Toolbar("Go", 0x3ff);

end;


' Get grating p[arameters from registry AlertRig\\(config)\\Gratings\\type$
func GetGratingParameters$(type$)
	var key$;
	var stmp$;
	key$ := "AlertRig\\" + GetCurrentConfiguration$() + "\\Gratings";
	Profile(key$, type$, "", stmp$);
	return stmp$;
end;


' Used to hard-quit from toolbar. This will close data files without saving!!! 
' It is important that this function be disabled once sampling is started!!!

func Quit%()
	FileClose(-1,-1);
	return 0;
end;

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func Start%()
	var CommandLine$;
	var FixationPoint$;
	var Extras$;
	var Distractors$:="";
	
	SampleStart(0); 'Start sampling
	
	GetFixationScriptParameters();


	if fixCalibrationFlag% = 0 then

		' Initialize the stimulus locator
		Center%();

		' Use distractors? 
		if fixStimulusFlag% = 1 then
			Extras$ := GetGratingParameters$("Stimulus");
			if Len(Extras$) = 0 then
				Message("Warning! Use stimulus flag is set, but stimulus grating is not configured!");
			else
				Distractors$ += " -g " + Extras$;
			endif
		endif
		
		if fixDistractor1Flag% = 1 then
			Extras$ := GetGratingParameters$("Distractor1");
			if Len(Extras$) = 0 then
				Message("Warning! Use distractor 1 flag is set, but distractor 1 grating is not configured!");
			else
				Distractors$ += " -g " + Extras$;
			endif
		endif
		
		if fixDistractor2Flag% = 1 then
			Extras$ := GetGratingParameters$("Distractor2");
			if Len(Extras$) = 0 then
				Message("Warning! Use distractor 2 flag is set, but distractor 2 grating is not configured!");
			else
				Distractors$ += " -g " + Extras$;
			endif
		endif
		
		if fixDistractor3Flag% = 1 then
			Extras$ := GetGratingParameters$("Distractor3");
			if Len(Extras$) = 0 then
				Message("Warning! Use distractor 3 flag is set, but distractor 3 grating is not configured!");
			else
				Distractors$ += " -g " + Extras$;
			endif
		endif
		
		if fixDistractor4Flag% = 1 then
			Extras$ := GetGratingParameters$("Distractor4");
			if Len(Extras$) = 0 then
				Message("Warning! Use distractor 4 flag is set, but distractor 4 grating is not configured!");
			else
				Distractors$ += " -g " + Extras$;
			endif
		endif
		
		Extras$ := " -b " + fixBackgroundColor$ + " -d " + DistanceToScreenMM$;
		
		CommandLine$ := "cmd /k " + bin$ + "\\Fixation " + FixationArg$ + Extras$ + Distractors$;
		
		'CommandLine$ :=bin$ + "\\dFixation " + FixationPoint$ + Distractors$ + Extras$;
	else

		Horizontal%();

		Extras$ := " -b " + fixBackgroundColor$ + " -d " + DistanceToScreenMM$ + " -C " + str$(fixCalibrationOffset);
		CommandLine$ := "cmd /k " + bin$ + "\\Calibration " + FixationArg$ + Extras$;

	endif

	'Begin the stimulus program. It will take a few seconds for the VSG to initialize.
	PrintLog(CommandLine$+"\n");
	StimHandle% := ProgRun(CommandLine$,1);
	if StimHandle% <0 then Message("Could not start stimulus."); halt; endif
'	Yield(5);	'Wait a few seconds to make sure the VSG has time to initialize.

	ToolbarSet(0,"",IdleProcessing%);	'Enable the IdleProcessing function
	ToolbarEnable(2,1);
	ToolbarEnable(1,0);
	ToolbarEnable(6,0);
	ToolbarEnable(7,1);
	ToolbarEnable(8,1);
	ToolbarEnable(9,1);
	ToolbarEnable(10,1);
	View(DataWindow%);
	Yield(.1);
	return 1;
end;


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func Stop%()

	' Disable idle process func
	ToolbarSet(0,"");

	' Tell stim app to shut down
	SampleKey("Q");
	Yield(.1);
	SampleStop();
	ProgKill(StimHandle%);
	while ProgStatus(StimHandle%) > 0 do
		Yield();
	wend
	ProgRun(bin$ + "\\ClearTrigger",0);

	' Increment daily totals and save then
	TotalTrials% += nTrialsStarted%;
	TotalRewards% += nTrialsSuccess%;
	SaveDailyTotals();

	' Save data file
	' djs 5-2-2005
	' When autonaming is used, this is not necessary. In fact, the call to FileSave fails!
	' Leaving the call out altogether makes it work. .. . . 
	'View(DataWindow%);
	'if FileSave() <> 0 then
	'	Message("WARNING! Data file not saved!!!");
	'endif

end;



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func PauseStimuli%();
Pause% :=1;
ToolbarEnable(4,0);
ToolbarEnable(5,1);
return 1;
end
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func ResumeStimuli%();
Pause% :=0;
ToolbarEnable(4,1);
ToolbarEnable(5,0);
return 1;
end


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func GetStimX()
	var xstim;
	if fixCalibrationFlag% = 0 then
		xstim := FixationX;
	else
		xstim := fixCalibrationOffset * (iStimX%[iStimIndex%]-2);
	endif
	return xstim;
end;

func GetStimY()
	var ystim;
	if fixCalibrationFlag% = 0 then
		ystim := FixationY;
	else
		ystim := fixCalibrationOffset * (iStimY%[iStimIndex%]-2);
	endif
	return ystim;
end;

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func InFixationWindow%(EyePosX,EyePosY)
	var i% := 0;
	var xstim, ystim;
	xstim := GetStimX();
	ystim := GetStimY();
	'Determine if the eye position is within the fixation window
	if (Pow(EyePosX-xstim, 2) + Pow(EyePosY-ystim, 2)) <= Pow(WindowRadius,2) then 
		i% := 1;
	endif
	return i%;
end;


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
func Reward%()
	var i;
	for i:=1 to JuicePerReward% do Yield(.05); SampleKey("J"); next
	return 1;
end;




'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func Horizontal%()
	iStimX%[0] := 1; iStimY%[0] := 2;
	iStimX%[1] := 1; iStimY%[1] := 2;
	iStimX%[2] := 2; iStimY%[2] := 2;
	iStimX%[3] := 2; iStimY%[3] := 2;
	iStimX%[4] := 3; iStimY%[4] := 2;
	iStimX%[5] := 3; iStimY%[5] := 2;
	iStimX%[6] := 2; iStimY%[6] := 2;
	iStimX%[7] := 2; iStimY%[7] := 2;
	return 1;
end;

func Vertical%()
	iStimX%[0] := 2; iStimY%[0] := 1;
	iStimX%[1] := 2; iStimY%[1] := 1;
	iStimX%[2] := 2; iStimY%[2] := 2;
	iStimX%[3] := 2; iStimY%[3] := 2;
	iStimX%[4] := 2; iStimY%[4] := 3;
	iStimX%[5] := 2; iStimY%[5] := 3;
	iStimX%[6] := 2; iStimY%[6] := 2;
	iStimX%[7] := 2; iStimY%[7] := 2;
	return 1;
end;

func Corners%()
	iStimX%[0] := 1; iStimY%[0] := 1;
	iStimX%[1] := 1; iStimY%[1] := 1;
	iStimX%[2] := 3; iStimY%[2] := 1;
	iStimX%[3] := 3; iStimY%[3] := 1;
	iStimX%[4] := 3; iStimY%[4] := 3;
	iStimX%[5] := 3; iStimY%[5] := 3;
	iStimX%[6] := 1; iStimY%[6] := 3;
	iStimX%[7] := 1; iStimY%[7] := 3;
	return 1;
end;

func Center%()
	iStimX%[0] := 2; iStimY%[0] := 2;
	iStimX%[1] := 2; iStimY%[1] := 2;
	iStimX%[2] := 2; iStimY%[2] := 2;
	iStimX%[3] := 2; iStimY%[3] := 2;
	iStimX%[4] := 2; iStimY%[4] := 2;
	iStimX%[5] := 2; iStimY%[5] := 2;
	iStimX%[6] := 2; iStimY%[6] := 2;
	iStimX%[7] := 2; iStimY%[7] := 2;
	return 1;
end;





'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters START
'
'
' WARNING! This file should be used with AlertRig scripts only!!!
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

var DistanceToScreenMM$;		' Distance to screen in MM
var DegreesPerVoltX;			' Conversion factor from ADC to visual degrees
var DegreesPerVoltY;			' Conversion factor from ADC to visual degrees
var EyeCoilSamplingFrequency%;		' Frequency to sample eye coil at
var NumberOfElectrodes%;		' Number of electrodes (i.e. wavemark inputs) in use
var JuicePerReward%;			' Number of juice squirts per reward
var EyeXPort%;                  ' Port horizontal eye signal connected to
var EyeYPort%;                  ' Port vertical eye signal connected to
var JoystickPort%;              ' Port joystick is connected to
var WavemarkPorts%[16];
var NumWavemarkPorts%;
var ContinuousPorts%[16];
var NumContinuousPorts%;

GetDAQParameters();

func GetJoystickPort%()
    return JoystickPort%;
end

func GetEyeXPort%()
    return EyeXPort%;
end

func GetEyeYPort%()
    return EyeYPort%;
end

func GetDistanceToScreenMM$()
    return DistanceToScreenMM$;
end

func GetDegreesPerVoltX()
    return DegreesPerVoltX;
end

func GetDegreesPerVoltY()
    return DegreesPerVoltY;
end

func GetNumberOfElectrodes%()
    return NumberOfElectrodes%;
end

func GetJuicePerReward%()
    return JuicePerReward%;
end

func GetWavemarkPorts%(ports%[])
    if Len(ports%[]) < NumWavemarkPorts% then return -1;    ' array not large enough
    else
        ArrConst(ports%[0:NumWavemarkPorts%], WavemarkPorts%[0:NumWavemarkPorts%]);
        return NumWavemarkPorts%;
    endif
end

func GetContinuousPorts%(ports%[])
    if Len(ports%[]) < NumContinuousPorts% then return -1;    ' array not large enough
    else
        ArrConst(ports%[0:NumContinuousPorts%], ContinuousPorts%[0:NumContinuousPorts%]);
        return NumContinuousPorts%;
    endif
end


func DAQParametersDialog%()

	var iReturn%;
	var idist%;

	GetDAQParameters();

	idist% := val(DistanceToScreenMM$);
	DlgCreate("DAQ parameters");
	DlgInteger(1, "Number of Electrodes:", 1, 7, 0, 0, 1);
	DlgInteger(2, "Screen Distance (MM):", 1, 5000);
	DlgReal(3, "Degrees/Volt (X):", 0, 100);
	DlgReal(4, "Degrees/Volt (Y):", 0, 100);
	DlgInteger(5, "Eye Coil Sampling Freq (Hz):", 1, 20000);
	DlgInteger(6, "Juice per reward:", 0, 10, 0, 0, 1);
    DlgInteger(7, "Eye X Port", 0, 15);
    DlgInteger(8, "Eye Y Port", 0, 15);
    DlgInteger(9, "Joystick Port", 0, 15);
	iReturn% := DlgShow(NumberOfElectrodes%, idist%, DegreesPerVoltX, DegreesPerVoltY, 
                        EyeCoilSamplingFrequency%, JuicePerReward%,
                        EyeXPort%, EyeYPort%, JoystickPort%);

	if iReturn% = 1 then
		DistanceToScreenMM$ := str$(idist%);
		SaveDAQParameters();
	endif
	return iReturn%;
end;




	
proc GetDAQParameters()
	var key$;
    var s$;

	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
    DistanceToScreenMM$ := GetStringRegistryValue$(key$, "DistanceToScreenMM", "850");
    DegreesPerVoltX := GetFloatRegistryValue(key$, "DegreesPerVoltX", 0.0);
    DegreesPerVoltY := GetFloatRegistryValue(key$, "DegreesPerVoltY", 0.0);
    EyeCoilSamplingFrequency% := GetIntRegistryValue%(key$, "EyeCoilSamplingFrequency", 1000);
    NumberOfElectrodes% := GetIntRegistryValue%(key$, "NumberOfElectrodes", 1);
    JuicePerReward% := GetIntRegistryValue%(key$, "JuicePerReward", 1);
    EyeXPort% := GetIntRegistryValue%(key$, "EyeXPort", 14);
    EyeYPort% := GetIntRegistryValue%(key$, "EyeYPort", 15);
    JoystickPort% := GetIntRegistryValue%(key$, "JoystickPort", 13);
    s$ := GetStringRegistryValue$(key$, "WavemarkPorts", "");
    NumWavemarkPorts% := ReadStr(s$, WavemarkPorts%[]);
    s$ := GetStringRegistryValue$(key$, "ContinuousPorts", "");
    NumContinuousPorts% := ReadStr(s$, ContinuousPorts%[]);
    
end;

proc SaveDAQParameters()
    var key$;
    var s$;
    var i%;
	key$ := GetRigName$() + "\\" + GetCurrentConfiguration$() + "\\DAQ";
    SetStringRegistryValue(key$, "DistanceToScreenMM", DistanceToScreenMM$);
    SetStringRegistryValue(key$, "DegreesPerVoltX", str$(DegreesPerVoltX));
	SetStringRegistryValue(key$, "DegreesPerVoltY", str$(DegreesPerVoltY));
	SetIntRegistryValue(key$, "EyeCoilSamplingFrequency", EyeCoilSamplingFrequency%);
	SetIntRegistryValue(key$, "NumberOfElectrodes", NumberOfElectrodes%);
	SetIntRegistryValue(key$, "JuicePerReward", JuicePerReward%);
    SetIntRegistryValue(key$, "EyeXPort", EyeXPort%);
    SetIntRegistryValue(key$, "EyeYPort", EyeYPort%);
    SetIntRegistryValue(key$, "JoystickPort", JoystickPort%);
    s$ := "";
    for i% := 0 to NumWavemarkPorts%-1 do
        s$ += str$(WavemarkPorts%[i%]) + ",";
    next
    SetStringRegistryValue(key$, "WavemarkPorts", s$);
    s$ := "";
    for i% := 0 to NumContinuousPorts%-1 do
        s$ += str$(ContinuousPorts%[i%]) + ",";
    next
    SetStringRegistryValue(key$, "ContinuousPorts", s$);    
end;

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'	DAQ Parameters END
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' $Id: AcuteFlash-Chapman.s2s,v 1.2 2012-10-23 21:06:13 devel Exp $


var hView,hExperiment;
var hDataWindow%;
var hHistWindow%;
var hHistCursor%;
var hRasterWindow%;
var hStimulus%;
var bgTotalCounts:=0;
var bgTotalTime:=0;
var bin$;
var nRead%;
var iScreenDistanceMM%;
var nChannels% := 1;
var nRepeats% := 1;
var TriggerChannel%;
var colorSel%[4];
var times[4];
var nFlash% := 0;
var sFlashArg$;     ' part of arg list for colorflash prog. 
var sPattern$;		  ' letters representing color pattern to stick in data file
var tPeriod;        ' period for a single cycle
var sFilePathRoot$;	' dir for current experiment. 
var sFileNameRoot$;	' base name for data files
var sParameterFilePath$;
var sParameterFileName$;
bin$ := View(App(3)).FileName$(1) + View(App(3)).FileName$(2) + "..\\bin";	' Location of executables is ..\bin, relative to script dir

' jump through a couple of hoops to determine what folder to store the data files in. 
'View(App(0)).Window(0,0,50,100);
hView := FileOpen("C:\\AcuteRig\\Experiments\\CurrentExperiment.par",8,0);
nRead% := Read(sFilePathRoot$);
FileClose();
if nRead% >0 then
	hExperiment := FileOpen(sFilePathRoot$+"\\Experiment.par",8,0);
	nRead% := Read(iScreenDistanceMM%);
	nRead% := Read(sFileNameRoot$);
	PrintLog("Data file path %s filename base %s\n", sFilePathRoot$, sFileNameRoot$);
	FileClose();
endif
View(SampleHandle(1)).WindowVisible(0);

' location for data files
sParameterFilePath$ := sFilePathRoot$ + "\\DataFiles\\";
PrintLog("Parameter file path is " + sParameterFilePath$ + "\n");

if AFDialog%() = 0 then
	halt;
endif


TriggerChannel% := nChannels% + 1;

' sampling config
CreateSampleConfiguration();


' open data window
hDataWindow%:=FileNew(0,4);
View(hDataWindow%).WindowVisible(1);
View(hDataWindow%).Window(0,0,100,50);
View(hDataWindow%).XRange(0, 30);
View(hDataWindow%);

' Open raster plot
hRasterWindow% :=  View(hDataWindow%).SetPSTH(1, 50, tPeriod/50, 0, 0, 2);
View(hRasterWindow%).WindowVisible(1);
View(hRasterWindow%).Window(0,50,100,100);


sParameterFilePath$ := FileName$(1) + FileName$(2);
sParameterFileName$ := FileName$(3) + FileName$(4) + ".par";

' Set toolbar and wait for the "go" signal
ToolbarSet(1,"Quit",Quit%);
ToolbarSet(2,"Sample start",Start%);
ToolbarSet(3,"Sample stop", Stop%);
ToolbarEnable(1, 0);
ToolbarEnable(2, 1);
ToolbarEnable(3, 0);
Toolbar("Go",1234);



'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' dialog to get parameters. 
' do some processing of parameters when done so we can use info later in script. 
func AFDialog%()
   var iret%;
	var i%, iStop%;
	times[0] := 1;
	times[1] := 1;
	times[2] := 1;
	times[3] := 1;
	DlgCreate("Stimulus Parameters");
	DlgInteger(1, "Number of channels: ",1,8);
	DlgInteger(2, "Number of repeats: ",1,100);
	DlgList(3, "Color 1", "NONE|Black|Gray|White", 4);   ' WARNING!!! HARD-CODED See WriteParameterFile()
	DlgReal(4, "Time(s) 1", 0.01, 100.0);                ' and values for const stateBlack% et al. 
	DlgList(5, "Color 2", "NONE|Black|Gray|White", 4);
	DlgReal(6, "Time(s) 2", 0.01, 100.0);
	DlgList(7, "Color 3", "NONE|Black|Gray|White", 4);
	DlgReal(8, "Time(s) 3", 0.01, 100.0);
	DlgList(9, "Color 4", "NONE|Black|Gray|White", 4);
	DlgReal(10, "Time(s) 4", 0.01, 100.0);
	iret% := DlgShow(nChannels%, nRepeats%,  colorSel%[0], times[0], colorSel%[1], times[1], colorSel%[2], times[2], colorSel%[3], times[3]);

   if iret% = 1 then
		sFlashArg$ := " -p ";
		tPeriod := 0;
		while i% < 4 and iStop% = 0 do
			if colorSel%[i%] = 0 then
				iStop% := 1;
			else
				nFlash% += 1;
				if i% > 0 then 
					sFlashArg$ += ",";
				endif
				docase 
				case colorSel%[i%] = 1 then
					sFlashArg$ += "0,0,0," + str$(times[i%]);
					sPattern$ += "B";
					tPeriod += times[i%];
				case colorSel%[i%] = 2 then
					sFlashArg$ += "0.5,0.5,0.5," + str$(times[i%]);
					sPattern$ += "G";
					tPeriod += times[i%];
				case colorSel%[i%] = 3 then
					sFlashArg$ += "1,1,1," + str$(times[i%]);
					sPattern$ += "W";
					tPeriod += times[i%];
				endcase
			   i% += 1;
			endif
		wend
   endif	
	
   return iret%;	
end

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func Quit%()
	return 0;
end;

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func Start%()
	var sCommand$;
	sCommand$ := bin$ + "\\colorflash -n -A -r " + str$(nRepeats%) + sFlashArg$;
	PrintLog("Command line: " + sCommand$ + "\n");
	SampleStart(0);
	SampleText(sPattern$);
	hStimulus% := ProgRun(sCommand$ , 1);
	Yield(5);
	ToolbarEnable(2, 0);
	ToolbarEnable(3, 1);
	ToolbarSet(0,"",IdleProcessing%);
	return 1;
end;

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func Stop%()
	if ProgStatus(hStimulus%) = 1 then ProgKill(hStimulus%); endif
	ProgRun(bin$ + "\\cleartrigger",0);
	ToolbarSet(0,"");
	SampleStop();

	' Write parameters to file for posterity
	WriteParameterFile();


	ToolbarEnable(3, 0);
	ToolbarEnable(1, 1);

	return 1; 
end; 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''







proc WriteParameterFile()
	var ParViewHandle;
	var i%;
	var stmp$[4];
	stmp$[0] := "NONE";
	stmp$[1] := "Black";
	stmp$[2] := "Gray";
	stmp$[3] := "White";
	FilePathSet(sParameterFilePath$);

	ParViewHandle := FileOpen(sParameterFilePath$ + sParameterFileName$,8,1);
	if ParViewHandle <0 then Message("Could not create parameter file "+sParameterFilePath$+sParameterFileName$);
	else
		Print("[stimulus]\n");
		Print("\"Number Of Channels\" %d\n",nChannels%);
		Print("\"Number of repeats\" %d\n",nRepeats%);
		for i% := 0 to 3 do
			Print("\"Color/Time %d: \" %s/%f\n", i%, stmp$[colorSel%[i%]], times[i%]);
	   next
	endif
end



''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
proc CreateSampleConfiguration()
var index;
var TimeLimit;
SampleClear(); 'Set standard sampling state
SampleAutoFile(1); 'Enable automatic file naming
SampleAutoName$(sFileNameRoot$ + "_chap_00");
' location for data files
FilePathSet(sFilePathRoot$+"\\DataFiles\\", 3);

'SampleAutoComment(1); 'Prompt for comment after sampling
SampleAutoCommit(60); 'Seconds between each file commit

'Channel recording definitions

for index := 1 to nChannels% do
	SampleWaveMark(index,index-1,2000,34,15,28000); ' chan, port, event rate, points, pre-trigger, rate
	SampleTitle$(index, "Chan " + str$(index));
next

SampleEvent(nChannels%+1,1,1,1000); 
SampleTitle$(nChannels%+1, "Trigger");

' text mark for recording pattern
SampleTextMark(16);

SampleOptimise(2,2,2,2,50);  'Set optimise mode (do this last)
SampleMode(1); 'Continuous sampling
end;


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

var tTrigger := 0;
var tTriggerLast := 0;
var nCountBlocks% := 0;		' this is the number of blocks COMPLETED
var nCountRepeats% := 0;	' this is the number of  b/w cycles COMPLETED
var iCycle% := -1;				' keep track of where in cycle we are; increment each trigger
const stateStim% := 10;
const stateBlack% := 1;
const stateGray% := 2;
const stateWhite% := 3;
const stateRepeatDone% := 4;
const stateBlockDone% := 5;
const stateStimDone% := 6;
var state% := stateStim%;
' When nCountBlocks% = nBlocks%  and nCountRepeats% = nRepeats% then the stimulus has run its course. 

var iStartCycle% := -1;
var iStartCycleTime;

func IdleProcessing%()
	var istatus% := 1;

	' Look for trigger and keep tally. Process for raster if we reached the end of a cycle.
	tTrigger := View(hDataWindow%).NextTime(TriggerChannel%, tTriggerLast);
	if tTrigger > 0 then
		iCycle% += 1;
		if iCycle% mod nFlash% = 0 then
			if iCycle% > 0 then 	
				View(hRasterWindow%).Process(iStartCycleTime, iStartCycleTime + tPeriod, 0, 1);
				PrintLog("Process icycle %d start %f end %f\n", iCycle%, iStartCycleTime, iStartCycleTime+tPeriod);
			endif
			iStartCycleTime := tTrigger;
		endif

		tTriggerLast := tTrigger;
	endif 	' if tTrigger > 0
	return istatus%;

end

proc HandleSpikes(trigger)

	View(hHistWindow%).Process(trigger, 0, 0, 1);

end

proc HandleBackgroundSpikes(tstart, tstop)

	var c;
	c := View(hDataWindow%).Count(1, tstart, tstop);

	bgTotalCounts += c;
	bgTotalTime += (tstop - tstart);

	if (bgTotalTime > 0) then
		View(hHistWindow%);
		HCursor(hHistCursor%, bgTotalCounts/bgTotalTime);
	endif
end

#include "../../Spike2Util/UsreyUtil.s2s"

var grutilDisable%[11];
var grutilNDisable% := 0;


func SaveGratingParameters(type$, gr$)
    var key$;
    key$ := GetRegistryKey$(1) + "\\Gratings";
    SetStringRegistryValue(key$, type$, gr$);
    return 0;
end


' Get grating parameters from registry AlertRig\\(config)\\Gratings\\type$
func GetGratingParameters$(type$)
	var key$;
	var stmp$;
	key$ := GetRegistryKey$(1) + "\\Gratings";
	return GetStringRegistryValue$(key$, type$, "");
end;


' GratingParametersDialog%
'
' Presents a dialog to edit grating parameters. You can disable certain pieces of the dialog to prevent changes to those values. If the 
' user hits OK then the grating parameter string is returned in param$ (this is the value saved in the registry and passed to VSG apps
' to specify gratings). In addition, each of the individual parameter values is returned in the various parameters. 
' 
' disable%[] is an array with ndisable% elements. Each element should be an integer between 1 and 11 - these represent dialog items that should
' be disabled -- i.e. the user may not modify them. You still must provide a value for them when calling GratingParametersDialog, however; the 
' values will be visible but grayed out. The dialog item numbers are as follows:
'
' 	1: X coordinate
'   2: Y coordinate
'   3: Width
'   4: Height
'   5: Contrast %
'   6: Spatial freq
'   7: Temporal freq
'   8: Orientation (degrees)
'   9: Color
'   10: Pattern (sine, square wave)
'   11: Aperture (oval, rectangular)
'
' Returns 1 (0) if the user hits OK (Cancel). 


func GratingParametersDialog%(label$, &param$, disable%[], ndisable%, &x, &y, &w, &h, &contrast%, &sf, &tf, &ori, &cv$, &pattern$, &aperture$)

	var i%;

	var ColorVectorList$[5];
	var cvl$[5];
	var icvl% := 0;
	ColorVectorList$[0]:="Black/White";	cvl$[0]:="b";
	ColorVectorList$[1]:="L cone";		cvl$[1]:="l";
	ColorVectorList$[2]:="M cone";		cvl$[2]:="m";
	ColorVectorList$[3]:="S cone";		cvl$[3]:="s";
	ColorVectorList$[4]:="Custom";		cvl$[4]:="custom";

	var PatternList$[2];
	var pl$[2];
	var ipl% := 0;
	PatternList$[0]:="Sine";	pl$[0]:="s";
	PatternList$[1]:="Square";	pl$[1]:="q";

	var ApertureList$[2];
	var al$[2];
	var ial% := 0;
	ApertureList$[0]:="Ellipse";	al$[0]:="e";
	ApertureList$[1]:="Rectangle";	al$[1]:="r";

    ' If the passed params$ is non-empty, then parse it before opening the dialog. 
    if Len(param$)>0 then
        if ParseGratingParameters%(param$, x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$) = 0 then
            FindMatchedStringIndex%(cv$, cvl$[], 5, 4);
            FindMatchedStringIndex%(pattern$, pl$[], 2, 0);
            FindMatchedStringIndex%(aperture$, al$, 2, 0);
        endif
    endif

    ' Prepare global var to handle disabled vars in the check function. 
    for i% := 0 to ndisable%-1 do
        grutilDisable%[i%] := disable%[i%];
    next;
    grutilNDisable% := ndisable%;


	DlgCreate("Grating parameters: " + label$, 25, 10);
    DlgAllow(0, 0, GratingParametersDialogCheck%);
	DlgReal(1, "X:", -40, 40);
	DlgReal(2, "Y:", -40, 40);
	DlgReal(3, "Width:", .1, 30);
	DlgReal(4, "Height:", .1, 30);
	DlgInteger(5, "Contrast(%):", -100, 100);
	DlgReal(6, "Spatial Freq:", .1, 10);
	DlgReal(7, "Temporal Freq:", 0, 10);
	DlgReal(8, "Orientation (degrees):", 0, 360);
	DlgList(9, "Color:", ColorVectorList$[]);
	DlgList(10, "Pattern:", PatternList$[]);
	DlgList(11, "Aperture:", ApertureList$[]);
	i% := DlgShow(x, y, w, h, contrast%, sf, tf, ori, icvl%, ipl%, ial%);
	if i%=1 then
		cv$ := cvl$[icvl%];
		pattern$ := pl$[ipl%];
		aperture$ := al$[ial%];
		' save parameters
		param$ := Print$("%.1f,%.1f,%.1f,%.1f,%d,%.2f,%.2f,%.1f,%s,%s,%s", x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$);
	endif
    return i%;
end;


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' GratingParametersDialogCheck%(item%)
' 
' Check function for the grating parameters dialog.
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func GratingParametersDialogCheck%(item%)
    var status% := 1;
    var i%;
    
    docase 
    case item% = 0 then
        for i% := 0 to grutilNDisable%-1 do
            DlgEnable(0, grutilDisable%[i%]);
        next
    endcase
    
    return status%;
end


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' ParseGratingParameters%(gr$, &x, &y, &w, &h, &contrast%, &sf, &tf, &ori%, &cv$, &pattern$, &aperture$)
' 
' Given a grating parameter list, parse the individual values in to the given variable list. 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func ParseGratingParameters%(gr$, &x, &y, &w, &h, &contrast%, &sf, &tf, &ori, &cv$, &pattern$, &aperture$)
    var status% := 0;
    var n%;
	ReadSetup("", "", "", "", ",");
	n% := ReadStr(gr$, x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$);
    ReadSetup();
    if n% <> 11 then status% := -1; endif;
    return n%;
end


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' ParseGratingParametersBrief%(gr$, &x, &y, &w, &h);
' 
' Given a grating parameter list, parse the individual values in to the given variable list. 
' Simpler version than the full-blown one, as it doesn't return all values. 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func ParseGratingParametersBrief%(gr$, &x, &y, &w, &h)
    var status% := 0;
    var n%;
	ReadSetup("", "", "", "", ",");
	n% := ReadStr(gr$, x, y, w, h);
    ReadSetup();
    if n% <> 4 then status% := -1; endif;
    return n%;
end


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' EncodeGratingParameters$(x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$)
'
' Form a parameter string for a grating with the given parameters. The resulting string
' is suitable for feeding to a VSG application as a parameter arg for a grating. 
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func EncodeGratingParameters$(x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$)
    return Print$("%.1f,%.1f,%.1f,%.1f,%d,%.2f,%.2f,%.1f,%s,%s,%s", x, y, w, h, contrast%, sf, tf, ori, cv$, pattern$, aperture$);
end;
 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' FindMatchedStringIndex%(val$, list$[], nlist%, default%)
'
' Given a string value, find the index of an array item that matches it. The first nlist% 
' items in the array list$[] are checked sequentially until a match is found. 
' 
' Returns the index of the match found. If no match is found then the index value in default% 
' is returned. The default value can be outside the range allowed for the list 
' (e.g. it can be negative). 
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    
func FindMatchedStringIndex%(val$, list$[], nlist%, default%)
    var i% := 0;
    var isFound% := 0;
    var index%;
    index% := default%;
    while isFound% = 0 and i% < nlist% do
        if list$[i%] = val$ then
            index% := i%;
            isFound% := 1;
        else
            i% += 1;
        endif
    wend
    return index%;
end

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' FindMatchedIntIndex%(val%, list%[], nlist%, default%)
'
' Same as FindMatchedStringIndex$, but works on a list of ints in a list. Remember, 
' the val% is matched against the elements in the array, but the returned value
' is an index into the array, not the value itself! 
' 
' Returns the index of the match found. If no match is found then the index value in default% 
' is returned. The default value can be outside the range allowed for the list 
' (e.g. it can be negative). 
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    
func FindMatchedIntIndex%(val%, list%[], nlist%, default%)
    var i% := 0;
    var isFound% := 0;
    var index%;
    index% := default%;
    while isFound% = 0 and i% < nlist% do
        if list%[i%] = val% then
            index% := i%;
            isFound% := 1;
        else
            i% += 1;
        endif
    wend
    return index%;
end



''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' ParseCustomColor%(s$, rgb%[])
' 
' Attempts to parse an rgb custom color in the format (r/g/b). Each value is assumed to be an int
' between 0 and 255, but no check is made of that here. The array rgb%[] must have at least 3 
' elements. 
' 
' Returns 0 if successful (3 numbers found, correct format) or -1 otherwise. 
'
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''


func ParseCustomColor%(s$, rgb%[])
    var status% := -1;
    var n;
    ReadSetup("", "/(", "(", ")");
    n := ReadStr(s$, rgb%[0], rgb%[1], rgb%[2]);
    ReadSetup();
    if n = 3 then
        status% := 0;
    endif
    return status%;
end


''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
' EncodeCustomColor$(rgb%[])
'
' Encode the rgb values (assumed to be between 0 and 255) in rgb%[] (there must be at least 3 
' elements) into a color string (r/g/b). 
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

func EncodeCustomColor$(r%, g%, b%)
    var s$;
    s$ := Print$("(%d/%d/%d)", r%, g%, b%);
    return s$;
end


